
group 'ru.itmo.idu'
version '2.0.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

repositories {
    maven {
        url 'https://repo.osgeo.org/repository/release/'
    }
    maven {
        url 'https://repo.boundlessgeo.com/main'
    }
    mavenLocal()
    mavenCentral()
    jcenter()
}

configurations {
    // com.vividsolutions.jts is migrated to org.locationtech.jts but some older libs do not know about it
    compile.exclude group: 'com.vividsolutions'
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

dependencies {
    compileOnly group: 'org.projectlombok', name: 'lombok', version: '1.18.10'
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: '1.18.10'

    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'


    compile group: 'org.locationtech.jts', name: 'jts-core', version: '1.18.2'
    compile group: 'org.locationtech.jts.io', name: 'jts-io-common', version: '1.18.2'

    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    compile group: 'org.geotools', name: 'gt-main', version: '26.4'
    compile group: 'org.geotools', name: 'gt-epsg-hsql', version: '26.4'
    compile group: 'org.geotools', name: 'gt-opengis', version: '26.4'
    compile group: 'org.geotools', name: 'gt-geojson', version: '26.4'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['geomUtilsPublication']
    pkg {
        version {
            name = '2.0.0'
            desc = 'New algorithms added'
            released  = new Date()
            vcsTag = '2.0.0'
        }
    }
}

// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "esmirnov"
            name "Egor Smirnov"
            email "evsmirnov@itmo.ru"
        }
    }

    scm {
        url "https://github.com/Urban-Research-Lab/jts-geometry-utils"
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir
    classifier "javadoc"
}

publishing {
    publications {
        geomUtilsPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'ru.itmo.idu'
            artifactId 'jts-geometry-utils'
            version '2.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Classes for common geometry manipulations and CRS projectsion, used in GIS development')
                root.appendNode('name', 'geometry-utils')
                root.appendNode('url', 'https://github.com/Urban-Research-Lab/jts-geometry-utils')
                root.children().last() + pomConfig
            }
        }
    }
}
